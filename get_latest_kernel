#!/usr/bin/env perl

use warnings;
use strict;

use feature qw/switch/;
no if $] >= 5.018, warnings => "experimental::smartmatch";
use Mojo::UserAgent;
use Mojo::DOM;
use List::Util qw/any/;
use File::Slurp qw/read_file/;
use POSIX qw/setsid/;
use Getopt::Long qw(:config gnu_compat no_permute no_getopt_compat pass_through);
use lib::abs 'lib';
use Linux::Kernel qw/name_from_link extract_version/;
use Linux::Kernel::Latest;
use Module::Loader;
use Try::Tiny;

BEGIN {
   eval {
      require Smart::Comments;
      Smart::Comments->import();
   }
}

my %config;
$config{working_dir} = '.';
$config{daemon} = 0;
$config{minutes} = 10;
$config{plugins} = [];

GetOptions(
   'daemon|d!'       => \$config{daemon},
   'minutes|m=i'     => \$config{minutes},
   'dir=s'           => \$config{working_dir},
   'load-plugins=s@' => \$config{plugins},
   'config=s'        => \$config{config},
) or die "Wrong command line arguments.\n";
$config{plugins} = [ split(/,/, join(',', @{$config{plugins}})) ];

unless (-d $config{working_dir} && -r _ && -x _) {
   die "Can't open directory $config{working_dir}\n"
}

if (exists $config{config} && ! -r $config{config}) {
   die "Can't read config file $config{config}\n"
}

#only plugin configuration options in config file
#in the form of "option=value"
foreach (read_file($config{config})) {
   chomp;
   if (/\A([\w-]++)=/) {
      my $arg = "--plugin-" . $_;
      unshift @ARGV, $arg
   } else {
      warn "Can't parse option '$_' in $config{config}. Skipping...\n"
   }
}

my $loader  = Module::Loader->new(max_depth => 1);
my @plugins;
my %pmap = map {lc(substr($_, rindex($_, ':') + 1)), $_} $loader->search('Linux::Kernel::Plugin');
foreach my $p (@{$config{plugins}}) {
   if (any {$p eq $_} keys %pmap) {
      ### LOADING PLUGIN: $p
      $loader->load($pmap{$p});
      push @plugins, $pmap{$p}->process_options(\%config, \@plugins);
   } else {
      die "There is no plugin: $p\n"
   }
}

@plugins = sort
   {
      die ("Can't use plugin " . (ref $a) . " and " . (ref $b) . " combined.\n")
         if $a->priority == $b->priority;
      $a->priority <=> $b->priority
   } @plugins;

if (@ARGV) {
   die "Unknown command line arguments: " . join(', ', @ARGV) . "\n"
}


### CONFIGURATION: %config

if ($config{daemon}) {
   my $pid = fork();
   die "can't fork: $!"
      unless defined $pid;
   if ($pid) {
      print "$pid\n";
      exit 0;
   }
   setsid();
   open (STDIN,  '</dev/null'); # TODO: logging
   open (STDOUT, '>/dev/null');
   open (STDERR, '>&STDOUT');
}


my $ua = Mojo::UserAgent->new(max_redirects => 3);

my $previous_link = '';
my $link;

CHECK:
$link = latest_kernel($ua);
if ($link && ($link ne $previous_link)) {
   my %opts = (link => $link, name => name_from_link($link), version => extract_version($link), ua => $ua);
   foreach (@plugins) {
      my $last = 0;

      try { # TODO:
         $_->action(\%opts)
      } catch {
         when (/^FAIL:/) { die $_ }
         default { $last = 1 }
      }
      last if $last;
   }
   $previous_link = $link
}

if ($config{daemon}) {
   sleep $config{minutes} * 60;
   goto CHECK
}

